group 'cristina.tech'
version '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'


jar {
    baseName = 'wanderlust'
    version = '1.0.0-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-data-rest')
    compile('joda-time:joda-time')
    compile('org.jadira.usertype:usertype.jodatime:2.0.1') // provides mappings for joda types to use in JPA entities

    // optional dependency if you want to load a HAL browser when you issue an HTTP GET to the root URL: http://localhost:9000/api/opentravel/
    // compile('org.springframework.data:spring-data-rest-hal-browser')

    runtime('org.flywaydb:flyway-core')
    runtime('org.postgresql:postgresql')
    runtime('com.zaxxer:HikariCP')

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('junit:junit:4.12')

    testRuntime('com.h2database:h2')
    // remove GSON test runtime after https://github.com/spring-projects/spring-boot/issues/6502 fix is released
    testRuntime('com.google.code.gson:gson')
}

jacoco {
    toolVersion = "0.7.7.201606060606"
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco.exec")
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}
build.dependsOn jacocoTestReport

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

tasks.coveralls {
    onlyIf { System.env.'CI' }
}
